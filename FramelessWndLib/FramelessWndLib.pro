# ----------------------------------------------------
# This file is generated by the Qt Visual Studio Tools.
# ------------------------------------------------------

QT       += core gui
greaterThan(QT_MAJOR_VERSION, 4): QT += widgets

TEMPLATE = lib
TARGET = FramelessWndLib
DEFINES += FRAMELESSWND_LIB
LIBS += -L"."
DEPENDPATH += .
MOC_DIR += .
UI_DIR += .
RCC_DIR += .
CONFIG(debug, debug|release){
    CONFIG += console
    DESTDIR = ../bin_debug/
    OBJECTS_DIR = ../obj/$${TARGET}/debug
}
CONFIG(release, debug|release){
    DESTDIR = ../bin_release/
    OBJECTS_DIR = ../obj/$${TARGET}/release
}
include(FramelessWnd.pri)
HEADERS += ./framelesswnd_global.h \
           widget.h

SOURCES += widget.cpp

win32 {
    # Only include / compile these files on Windows
    SOURCES += \
        FramelessWnd.cpp \
        winnativewindow.cpp

    HEADERS += \
        FramelessWnd.h \
        winnativewindow.h

    LIBS += -ldwmapi \
        -lgdi32
}

mac {
    # Only include / compile these files on OS X
    OBJECTIVE_SOURCES += \
        OSXHideTitleBar.mm
    HEADERS  +=\
        OSXHideTitleBar.h

    # Additionally include Cocoa for OS X code

    LIBS += -framework Foundation -framework Cocoa
    INCLUDEPATH += /System/Library/Frameworks/Foundation.framework/Versions/C/Headers
}
